{-
 - K-language grammar
 - by Piotr Dobrowolski 
 - (291528)
 -}

-- COMMENTS
comment "//";
comment "/*" "*/";
comment "#";

-- ENTRYPOINTS
entrypoints Program, Stm, Exp;


-- PROGRAM DEFINITION
Progr.  Program ::= [DGlobalDeclaration];
terminator nonempty DGlobalDeclaration "";



--GLOBALS
DFunction.  DGlobalDeclaration ::= Function_def;
DGlobal.    DGlobalDeclaration ::= Dec;
DNamespace. DGlobalDeclaration ::= Namespace;

NewFunction.    Function_def ::= Type_specifier Declarator "{" [StmOrDec] "}";

Declarators.    Dec ::= Type_specifier [Init_declarator] ";";
terminator nonempty Dec "";

NSDeclarator.  Namespace ::= "namespace" Ident "{" [Function_def] "}";
terminator Function_def "";



-- TYPES
rules Type_specifier ::= "int" | "float" | "bool" | Struct_spec;

Named.      Struct_spec ::= "struct" Ident "{" [Struct_dec] "}";
Unnamed.    Struct_spec ::= "struct" "{" [Struct_dec] "}";
Type.       Struct_spec ::= "struct" Ident;

Structen. Struct_dec ::= Type_specifier [Struct_declarator] ";";
terminator nonempty Struct_dec "";

StructOnlyDecl. Struct_declarator ::= Declarator;
StructInitDecl. Struct_declarator ::= Declarator ":" Constant_expression;
separator nonempty Struct_declarator ",";



-- DECLARATORS

VarName.        Declarator ::= Ident;
FunctionName.   Declarator ::= Declarator "(" [Parameter] ")";

SingleParam.    Parameter ::= Type_specifier Declarator; 
separator Parameter ",";

OnlyDecl.  Init_declarator ::= Declarator;
InitDecl.  Init_declarator ::= Declarator "=" Initializer;
separator nonempty Init_declarator ",";

InitExpr.   Initializer ::= Exp2;
InitList.   Initializer ::= "{" [Initializer] "}";
separator nonempty Initializer ",";



-- STATEMENTS
ListS.    Stm ::= "{" [StmOrDec] "}";
DecS.     Stm ::= DecStm;
ExprS.    Stm ::= ExpStm;
IfS.      Stm ::= If_stm;
IterS.    Stm ::= Iter_stm;
JumpS.    Stm ::= Jump_stm;
separator nonempty Stm "";

SStm.   StmOrDec ::= Stm;
SDec.   StmOrDec ::= Dec;
terminator StmOrDec "";

Sempty. ExpStm ::= ";";
Sexpr.  ExpStm ::= Exp ";";


Dempty. DecStm ::= ";";
Ddec.   DecStm ::= Dec ; --without ";" because Dec has it

SIf.        If_stm ::= "if" "(" Exp ")" Stm;
SIfElse.    If_stm ::= "if" "(" Exp ")" Stm "else" Stm;

SiterWhile. Iter_stm ::= "while" "(" Exp ")" Stm;
SiterFor.   Iter_stm ::= "for" "(" DecStm ExpStm Exp ")" Stm;

SjumpTwo.   Jump_stm ::= "continue" ";";
SjumpThree. Jump_stm ::= "break" ";";
SjumpFour.  Jump_stm ::= "return" ";";
SjumpFive.  Jump_stm ::= "return" Exp ";";



-- TOKENS
token CFloat (((digit+ '.' digit+)|(digit+ '.')|('.' digit+))(('e'|'E')('-')? digit+)?
                               ('f'|'F'))|((digit+ ('e'|'E')('-')? digit+)('f'|'F'));
token Boolean ('t''r''u''e' | 'f''a''l''s''e');


-- EXPRESSIONS
Ecomma.     Exp   ::= Exp "," Exp2;
Eassign.    Exp2  ::= Ident "=" Exp2;
Econdition. Exp3  ::= Exp4 "?" Exp ":" Exp3;
Elor.       Exp4  ::= Exp4 "||" Exp5;
Eland.      Exp5  ::= Exp5 "&&" Exp6;
Ebitor.     Exp6  ::= Exp6 "|" Exp7;
Ebitexor.   Exp7  ::= Exp7 "^" Exp8;
Ebitand.    Exp8  ::= Exp8 "&" Exp9;
Eeq.        Exp9  ::= Exp9 "==" Exp10;
Eneq.       Exp9  ::= Exp9 "!=" Exp10;
Elthen.     Exp10 ::= Exp10 "<" Exp11;
Egrthen.    Exp10 ::= Exp10 ">" Exp11;
Ele.        Exp10 ::= Exp10 "<=" Exp11;
Ege.        Exp10 ::= Exp10 ">=" Exp11;
Eleft.      Exp11 ::= Exp11 "<<" Exp12;
Eright.     Exp11 ::= Exp11 ">>" Exp12;
Eplus.      Exp12 ::= Exp12 "+" Exp13;
Eminus.     Exp12 ::= Exp12 "-" Exp13;
Etimes.     Exp13 ::= Exp13 "*" Exp14;
Ediv.       Exp13 ::= Exp13 "/" Exp14;
Emod.       Exp13 ::= Exp13 "%" Exp14;
Etypeconv.  Exp14 ::= "(" Type_specifier ")" Exp14;
Epreinc.    Exp15 ::= "++" Ident;
Epredec.    Exp15 ::= "--" Ident;
Epreop.     Exp15 ::= Unary_operator Exp14;
Efunk.      Exp16 ::= Ident "(" [Exp2] ")";
EfunkNS.    Exp16 ::= Ident "::" Ident "(" [Exp2] ")";
Eselect.    Exp16 ::= Exp16 "." Ident;
Epostinc.   Exp16 ::= Ident "++";
Epostdec.   Exp16 ::= Ident "--";
Evar.       Exp17 ::= Ident;
Econst.     Exp17 ::= Constant;

Efloat.     Constant ::= Double;
Ecfloat.    Constant ::= CFloat;
Ebool.      Constant ::= Boolean;
Eint.       Constant ::= Integer;  

Especial. Constant_expression ::= Exp3;

coercions Exp 17;
separator Exp2 ",";

Negative.    Unary_operator ::= "-";
Logicalneg.  Unary_operator ::= "!";
